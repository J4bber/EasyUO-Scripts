;-----------------------------------------------------------
; Script Name: TrailMyx's World Item Finder
; Author: TrailMyx
; Version: 0.21
; Client Tested with: 6.0.1.6
; EUO version tested with: 1.5 118
; Shard: OSI, FS(?)
; Revision Date:  62/2/2009
; Public Release: ...
; Global Variables Used: None
; Purpose/Features:
;    Use this script to warn you about world items you may be looking for while wandering around.
;  Just type in the partial name of the item/object/npc that you are looking for and the script
;  will locate it for you.
; 
;  If you select the line item, the distance will be computed and displayed.  This distance will
;  update as you walk.
;-----------------------------------------------------------

set %version v0.21
set #LPC 10
set %warn #FALSE
set %enable #FALSE
set %active #FALSE
gosub showEUOMenu1
set #MENUBUTTON N/A
set %timer #SCNT + 300 ; 5 minute reset
set %display_update #SCNT + 1

set %font_arial MS , #SPC , Ariel
menu delete EUOListBox1
gosub TM_CreateList List EUOListBox1 4 36 333 93 White WindowText %font_arial 8

loop1:
  if #MENUBUTTON = EUOButtonStart
  {
    set #MENUBUTTON N/A
    if %enable = #FALSE
    {
      set %enable #TRUE
      menu Font BGColor BtnFace
      menu delete EUOButtonStart
      menu Button EUOButtonStart 184 4 75 25 Stop
      ignoreitem reset
      gosub TM_DeleteAllItemsInList EUOListBox1
      gosub TM_ReDrawList EUOListBox1
    }
    else
    {
      set %enable #FALSE
      menu Font BGColor BtnFace
      menu delete EUOButtonStart
      menu Button EUOButtonStart 184 4 75 25 Start
    }
  }
  if %enable = #FALSE
    goto loop1
  finditem * *
  if #FINDKIND <> -1
  {
    if %active = #FALSE
    {
      set #LPC 1000
      menu delete EUOShape1
      menu delete EUOLabel1
      menu Shape EUOShape1 112 136 133 33 3 7 1 Black 7 Red
      menu Font BGColor Red
      menu Text EUOLabel1 168 144 Active
      set %active #TRUE
    }
    event property #FINDID
    menu get EUOEditTextIn
    if #MENURES <> %current_search || #SCNT > %timer
    {
      set %current_search #MENURES
      set %timer #SCNT + 300 ; 5 minute reset
      ignoreitem reset
    }
    if #MENURES in #PROPERTY
    {
      set %loc #FINDX , _ , #FINDY
      gosub TM_AddToList EUOListBox1 #PROPERTY %loc
    }
    ignoreitem #FINDID
    set %warn #FALSE
  }
  else
  {
    if %warn = #FALSE
    {
      set #LPC 10
      menu delete EUOShape1
      menu delete EUOLabel1
      menu Shape EUOShape1 112 136 133 33 3 7 1 Black 7 Lime
      menu Font BGColor Lime
	    menu Text EUOLabel1 168 144 Idle
      sound
      set %warn #TRUE
      set %active #FALSE
    }
  }
  menu get EUOListBox1
  if #MENURES > 0
  {
    set %arg #MENURES - 1
    gosub TM_GetIndexedTag EUOListBox1 %arg
    str pos #RESULT _
    if #STRRES > 0 && #SCNT >= %display_update
    {
      set %pos #STRRES - 1
      str left #RESULT %pos
      set %xval #STRRES
      set %pos %pos + 1
      str del #RESULT 1 %pos
      set %yval #STRRES
      set %xval #CHARPOSX - %xval
      set %yval #CHARPOSY - %yval
      set %text Dist: , #SPC , %xval , #SPC , %yval
      menu delete EUOLabelDistance
      menu Font BGColor BtnFace
      menu Text EUOLabelDistance 268 12 %text
      set %display_update #SCNT + 1
    }
  }
  goto loop1
;-----------------------------------------------------------------
sub TM_CreateList
  namespace push
  namespace local TM_window_list_ , %2
  namespace clear
  set !listtype %1
  set !listname %2
  set !x1 %3
  set !y1 %4
  set !x2 %5
  set !y2 %6
  set !bgcolor %7
  set !txtcolor %8
  set !font %9
  set !fontsize %10
  if %0 >= 11
    set !fontstyle %11
  else
    set !fontstyle N/A
  set !draw #TRUE

  if NODRAW in !listtype
    set !draw #FALSE

  if List in !listtype
    set !listtype List
    
  if Combo in !listtype
    set !listtype Combo

  if !draw = #TRUE
    gosub TM_DrawList !listname
  set !list_ptr 0
  namespace pop
return
;-----------------------------------------------------------------
sub TM_DrawList
  namespace push
  namespace local TM_window_list_ , %1
  set !temp_LPC #LPC
  set #LPC 1000
  set !listname %1
  menu Font BGColor !bgcolor
  menu Font Color !txtcolor
  menu Font Size !fontsize
  menu Font Name !font
  if !fontstyle = N/A
    menu Font Style
  else
    menu Font Style !fontstyle
  menu !listtype create !listname !x1 !y1 !x2 !y2
  set !end !list_ptr - 1
  if !end >= 0
  {
    for !i 0 !end
      menu !listtype add !listname !list . !i
  }
  set #LPC !temp_LPC
  namespace pop
return
;-----------------------------------------------------------------
sub TM_ReDrawList
  namespace push
  namespace local TM_window_list_ , %1
  menu delete %1
  gosub TM_DrawList %1
  namespace pop
return
;-----------------------------------------------------------------
sub TM_GetIndexedTag
  namespace push
  namespace local TM_window_list_ , %1
  set #RESULT !tag . %2
  namespace pop
return #RESULT
;-----------------------------------------------------------------
sub TM_AddToList
  namespace push
  namespace local TM_window_list_ , %1
  set !listname %1
  set !item %2
  set !tag %3
  set !list . !list_ptr !item
  set !tag . !list_ptr !tag
  set !list_ptr !list_ptr + 1
  set #RESULT !list_ptr
  menu !listtype add !listname !item
  namespace pop
return #RESULT
;-----------------------------------------------------------------
sub TM_DeleteAllItemsInList
  namespace push
  namespace local TM_window_list_ , %1
  set !temp_LPC #LPC
  set #LPC 1000
  set !listname %1
  set !redraw %2
  if !list_ptr >= 0
  {
    for !i 0 !list_ptr
    {
      set !list . !i N/A
      set !tag . !i N/A
    }
    set !list_ptr 0
    if !redraw = #TRUE
      gosub TM_ReDrawList !listname
  }
  set #LPC !temp_LPC
  namespace pop
return #RESULT 
;--------- EasyUO Menu Designer Code Begin ---------
sub showEUOMenu1
	menu Clear
	menu Window Title TrailMyx's World Item Locator , #SPC , %version
	menu Window Color BtnFace
	menu Window Size 352 174
	menu Font Transparent #true
	menu Font Align Right
	menu Shape EUOShape1 112 136 133 33 3 7 1 Black 7 Lime
	menu Font Name MS Sans Serif
	menu Font Size 8
	menu Font Style
	menu Font Color WindowText
	menu Font Transparent #false
	menu Font Align Left
	menu Font BGColor Lime
	menu Text EUOLabel1 168 144 Idle
	menu Font BGColor BtnFace
	menu Text EUOLabel2 4 8 Text
	menu Text EUOLabelDistance 268 12 Dist: 0 0
	menu Font BGColor Window
	menu List Create EUOListBox1 4 36 333 93
	menu Edit EUOEditTextIn 32 4 129
	menu Font BGColor BtnFace
	menu Button EUOButtonStart 184 4 75 25 Start
	menu Show 421 270
return
;--------- EasyUO Menu Designer Code End ---------

